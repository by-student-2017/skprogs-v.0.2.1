#!/usr/local/bin/gnuplot -persist
#------------------------------------------------
set terminal pngcairo color enhanced font "Arial" fontscale 1.0
set output "evplot.png"
#------------------------------------------------
set key opaque box lc rgb "white" height 1
#-----------------------------------------------
set title "Murnaghan equation of state"
set xlabel "Volume, V / Bohr^3"
set ylabel "Total Energy, E / Ha"
set mxtics 5
set mytics 5
#-----------------------------------------------
# Murnaghan equation of state
f(x) = (a*x/(b*(b-1)))*(b*(1-c/x)+(c/x)**b-1)+d
#-----------------------------------------------
a=0.001 # Bulk modulus [Ha/bohr^3] = 2*[Ry/bohr^3]: a * 29421.02648438959 [GPa]: FCC*4
b=2     # Differential of bulk modulus
c=1100  # Unit cell volume [bohr^3]
d=-7.8  # Total energy in the volume of a stable unit cell [Ha] = 2*[Ry] = 27.21*[eV]
#-----------------------------------------------
fit f(x) 've.data' using 1:2 via a,b,c,d
plot f(x), 've.data' using 1:2
#-----------------------------------------------
# ve.data: V(bohr^3) vs. Etot(Ha)
#-----------------------------------------------